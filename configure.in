dnl Process this file with autoconf to produce a configure script.
AC_INIT(gtk.h)

dnl Configure glib
AC_CONFIG_SUBDIRS(glib)

dnl Specify a configuration file
AC_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST

dnl Check to see if we are using GNU make
AC_MSG_CHECKING(for gmake)
AC_CACHE_VAL(gtk_cv_make,
[_GMAKE_=""
_MAKE_=""

gtk_cv_make=""
for trial in gmake gnumake make; do
  temp=`$trial -v 2>/dev/null | grep GNU`
    if test "x$temp" != "x"; then
      gtk_cv_make="$trial"
      break
    fi
done])

if test "x$gtk_cv_make" = "x"; then
  _GMAKE_="# "
  AC_MSG_RESULT(no)
else
  _MAKE_="# "
  AC_MSG_RESULT($gtk_cv_make)
fi

MAKE="$gtk_cv_make"
AC_SUBST(_GMAKE_)
AC_SUBST(_MAKE_)

dnl Make sure the `CFLAGS' variable is empty. This environment
dnl variable affects some of the tests because it affects the
dnl options during compiling. On some machines it can have a
dnl negative affect.
CFLAGS=""

dnl Checks for programs.
AC_PROG_CC
AC_PROG_RANLIB
AC_PROG_INSTALL
AC_PROG_LN_S

dnl Check to see if make needs to be set
AC_PROG_MAKE_SET

dnl Find the X11 include and library directories
AC_PATH_XTRA

AC_SUBST(X_CFLAGS)
AC_SUBST(X_LDFLAGS)
AC_SUBST(LIBS)

CFLAGS="$X_CFLAGS"
LDFLAGS="$X_LDFLAGS $X_LIBS"

dnl Checks for libraries.
dnl Check for the X11 library
AC_CHECK_LIB(X11, XOpenDisplay, LIBS="-lX11 $X_EXTRA_LIBS", no_x11_lib=yes, $X_EXTRA_LIBS)

dnl Check for the Xext library (needed for XShm extention)
AC_CHECK_LIB(Xext, XShmAttach, LIBS="-lXext $LIBS", no_xext_lib=yes, -lX11)

dnl Check for shared memory
AC_CHECK_HEADER(sys/ipc.h, AC_DEFINE(HAVE_IPC_H), no_sys_ipc=yes)
AC_CHECK_HEADER(sys/shm.h, AC_DEFINE(HAVE_SHM_H), no_sys_shm=yes)

dnl Check for the X shared memory extension header file
AC_MSG_CHECKING(X11/extensions/XShm.h)
if eval "test x$no_ext_lib = xyes"; then
  AC_MSG_RESULT(no)
  no_xshm=yes
else
  if eval "test -f $x_includes/X11/extensions/XShm.h"; then
    AC_MSG_RESULT(yes)
    AC_DEFINE(HAVE_XSHM_H)
  else
    AC_MSG_RESULT(no)
    no_xshm=yes
  fi
fi

AC_C_CROSS

dnl Check for private display resource base variable
AC_MSG_CHECKING(resource base field in XDisplay)
AC_CACHE_VAL(gtk_cv_display_resource_base,
[AC_TRY_RUN([
#define XLIB_ILLEGAL_ACCESS
#include <X11/Xlib.h>

int
main ()
{
  Display *display;

  return 0;

  display->resource_base;
}],
gtk_cv_display_resource_base="resource_base",
gtk_cv_display_resource_base="private3")])
AC_MSG_RESULT($gtk_cv_display_resource_base)
AC_DEFINE_UNQUOTED(RESOURCE_BASE, gdk_display->$gtk_cv_display_resource_base)

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_CHECK_FUNC(select, AC_DEFINE(HAVE_SELECT))

dnl Check for c compiler flags
AC_MSG_CHECKING(for compiler flags)
AC_CACHE_VAL(gtk_cv_syscflags,
[gtk_cv_syscflags=""
if eval "test x$GCC = xyes"; then
  gtk_cv_syscflags="-g -pipe -Wall -ansi -pedantic"
else
  rm -fr conftestdir
  if mkdir conftestdir; then
    cd conftestdir

    cat > Imakefile <<'EOF'
gtk_cv_syscflags:
	@echo gtk_cv_syscflags=\"${CFLAGS}\"
EOF

    if (xmkmf) >/dev/null 2>/dev/null && test -f Makefile; then
      eval `${MAKE-make} gtk_cv_syscflags 2>/dev/null | grep -v make`
    fi

    cd ..
    rm -fr conftestdir
  fi
fi])

dnl Remove optimization flags for now and replace with the debugging flag
SYSCFLAGS="-g"
for option in $gtk_cv_syscflags; do
  case $option in
    -g|-O|-O1|-O2|-O3|-O4|-O5|-O6) ;;
    *) SYSCFLAGS="$SYSCFLAGS $option" ;;
  esac
done

AC_MSG_RESULT($SYSCFLAGS)
AC_SUBST(SYSCFLAGS)

dnl Determine how to make shared libraries
AC_MSG_CHECKING(for shared library ability)
rm -fr conftestdir
if mkdir conftestdir; then
  cd conftestdir

  cat > Imakefile <<'EOF'
#include <Library.tmpl>

#ifdef HasSharedLibraries

shared:
	@echo has_shared_libraries=\"yes\"
	@echo shared_data_separation=\"SharedDataSeparation\"
	@echo shared_code_def=\"SharedCodeDef\"
	@echo shared_library_def=\"SharedLibraryDef\"
	@echo shared_library_ldflags=\"SharedLibraryLoadFlags\"
	@echo pic_cflags=\"PositionIndependentCFlags\"
	@echo extra_ldflags=\"ExtraLoadOptions\"

#else

shared:
	@echo has_shared_libraries=\"no\"
	@echo shared_data_separation=\"\"
	@echo shared_code_def=\"\"
	@echo shared_library_def=\"\"
	@echo shared_library_ldflags=\"\"
	@echo pic_cflags=\"\"
	@echo extra_ldflags=\"\"

#endif
EOF

  if (xmkmf) >/dev/null 2>/dev/null && test -f Makefile; then
    eval `${MAKE-make} shared 2>/dev/null | grep -v make`
  fi

  cd ..
  rm -fr conftestdir
fi

AC_MSG_RESULT($has_shared_libraries)
AC_SUBST(shared_data_separation)
AC_SUBST(shared_code_def)
AC_SUBST(shared_library_def)
AC_SUBST(shared_library_ldflags)
AC_SUBST(pic_cflags)
AC_SUBST(extra_ldflags)

shared_library_suffix="so"

if test "x$has_shared_libraries" = "xyes"; then
  _SHLIBS_=""

  case $host_os in
    aix*)     shared_library_suffix="so";;
    hpux*)    shared_library_suffix="sl";;
    irix*)    shared_library_suffix="so";;
    linux*)   shared_library_suffix="so";;
    netbsd*)  shared_library_suffix="so";;
    freebsd*) shared_library_suffix="so";;
    sunos*)   shared_library_suffix="so";;
    ultrix*)  shared_library_suffix=".o";;
  esac
else
  _SHLIBS_="#"
fi

AC_SUBST(shared_library_suffix)
AC_SUBST(_SHLIBS_)

AC_OUTPUT(Makefile Makefile.shared tests/Makefile)
